apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
        packageName "net.sourcewalker.ndkdemo"

        ndk {
            moduleName "ndkdemo"
            cFlags "-std=c99"
            ldLibs "log"
        }
    }

    buildTypes {
        debug {
            jniDebugBuild true
        }
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    productFlavors {
        x86 {
            ndk {
                abiFilter "x86"
            }

            // this is the flavor part of the version code.
            // It must be higher than the arm one for devices supporting
            // both, as x86 is preferred.
            versionCode = 3
            packageName defaultConfig.packageName + ".x86"
        }
        arm {
            ndk {
                abiFilter "armeabi-v7a"
            }
            versionCode = 2
            packageName defaultConfig.packageName + ".arm"
        }
        mips {
            ndk {
                abiFilter "mips"
            }
            versionCode = 1
            packageName defaultConfig.packageName + ".mips"
        }
        fat {
            // fat binary, lowest version code to be
            // the last option
            versionCode = 0
        }
    }

    // make per-variant version code
    applicationVariants.all { variant ->
        // get the single flavor
        def flavorVersion = variant.productFlavors.get(0).versionCode

        // set the composite code
        variant.mergedFlavor.versionCode = flavorVersion * 1000000 + defaultConfig.versionCode
    }
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.2.1'
}
